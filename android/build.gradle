buildscript {
    ext.kotlin_version = '2.0.10'
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop) {
    return rootProject.ext.has(prop) ?
            rootProject.ext.get(prop) : rootProject.ext.has("RNAudioPro_" + prop) ?
            rootProject.ext.get("RNAudioPro_" + prop) : project.properties["RNAudioPro_" + prop]
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion')
    namespace 'co.evergrace.audiopro'

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion')
        targetSdkVersion safeExtGet('targetSdkVersion')

        versionCode 1001
        versionName '0.0.1'

        consumerProguardFiles 'proguard-rules.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        // Make sure you have installed the react-native npm package before compiling
        url '../node_modules/react-native/android'
    }

    mavenCentral()
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-android:+"

    // Make sure we're using androidx
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
    implementation "androidx.lifecycle:lifecycle-process:2.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3"

    // kotlinaudiopro dependencies
    implementation 'io.coil-kt:coil:2.2.0'
    implementation 'androidx.media:media:1.6.0'
    api 'com.google.android.exoplayer:exoplayer:2.19.0'
    api 'com.google.android.exoplayer:extension-mediasession:2.19.0'
    api 'com.jakewharton.timber:timber:5.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
